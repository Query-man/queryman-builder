apply from: './libraries.gradle'

group 'org.queryman'
version '1.0.0'

description "Queryman Builder. Builder SQL for PostgreSQL"

ext {
    majorVersion = version.split('-')[0]
    domain = 'http://queryman.org'
}

allprojects {
    apply plugin: 'java'
    apply plugin: 'maven'
    apply plugin: 'signing'
    apply plugin: 'maven-publish'

    repositories {
        mavenCentral()
        mavenLocal()
        jcenter()
    }
    //common dependencies
    dependencies {
        testImplementation libraries.junitApi
        testRuntimeOnly libraries.junitEngine
    }

    test {
        useJUnitPlatform()
    }
}

subprojects { subProject ->

    String moduleName = subProject.name
    if (moduleName.equals('documentation')) {
        return
    }
    archivesBaseName = moduleName


    publishing {
        publications {
            mavenJava(MavenPublication) {
                pom.withXml {
                    def root = asNode()

                    root.appendNode('description', rootProject.description)
                    root.appendNode('url', rootProject.domain)
                }

                groupId rootProject.group
                artifactId moduleName
                version rootProject.version

                from components.java
            }
        }
    }

    task javadocJar(type: Jar) {
        classifier = 'javadoc'
        from javadoc
    }

    task sourcesJar(type: Jar) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    artifacts {
        archives javadocJar, sourcesJar
    }

    signing {
        sign configurations.archives
    }

    jar {
        inputs.property("moduleName", moduleName)
        manifest {
            attributes(
                    'Automatic-module-name': moduleName,
            )

        }
    }
    uploadArchives {
        repositories {
            mavenDeployer {
                beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

                repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                    authentication(userName: ossrhUsername, password: ossrhPassword)
                }

                snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                    authentication(userName: ossrhUsername, password: ossrhPassword)
                }

                pom.project {
                    name 'Queryman Builder'
                    description rootProject.description
                    packaging 'jar'
                    url rootProject.domain

                    version rootProject.version
                    groupId rootProject.group


                    issueManagement {
                        system 'github'
                        url 'https://github.com/Query-man/queryman-builder/issues'
                    }

                    scm {
                        url 'https://github.com/Query-man/queryman-builder'
                        connection 'scm:git:https://github.com/Query-man/queryman-builder.git'
                        developerConnection 'scm:git:git@github.com:Query-man/queryman-builder.git'
                    }

                    licenses {
                        license {
                            name 'MIT License'
                            url 'https://github.com/Query-man/queryman-builder/blob/master/license.txt'
                        }
                    }

                    developers {
                        developer {
                            id 'timur_dbk'
                            name 'Timur Shaidullin'
                            email 'timur@shaidullin.net'
                        }
                    }
                }
            }
        }
    }
}